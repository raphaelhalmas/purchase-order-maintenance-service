managed implementation in class ZBP_I_PurchasingDocument unique;
//strict;

define behavior for ZI_PurchasingDocument alias PurchasingDocument
persistent table zdt_ekko
lock master
//authorization master ( instance )
etag master LastChangedAt
{
  mapping for zdt_ekko
  {
    PurchasingDocumentId = purg_doc_id;
    PurchasingDocument = ebeln;
    CompanyCode = bukrs;
    PurchasingDocumentCategory = bstyp;
    PurchasingDocumentType = bsart;
    Supplier = lifnr;
    PurchasingOrganization = ekorg;
    PurchasingGroup = ekgrp;
    DocumentCurrency = waers;
    PurchasingDocumentDate = bedat;
    PurchasingDocumentNetAmount = netwr;
    CreatedBy = created_by;
    CreatedAt = created_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }

  field ( readonly, numbering : managed ) PurchasingDocumentId;

  field ( readonly ) PurchasingDocumentNetAmount, DocumentCurrency, CreatedBy, CreatedAt, LastChangedBy, LastChangedAt;

  //field ( mandatory ) PurchasingDocumentType, Supplier, PurchasingDocumentDate,
  //DocumentCurrency, CompanyCode, PurchasingOrganization, PurchasingGroup;

  create;
  update;
  delete;

  internal action recalculateNetAmount;

  determination setDocumentNumber on save { create; }
  determination setDocumentCurrency on save { create; }

  action copyPurchasingDocument result [1] $self;

  association _PurchasingDocumentItem { create; }
}

define behavior for ZI_PurchasingDocumentItem alias PurchasingDocumentItem
persistent table zdt_ekpo
lock dependent by _PurchasingDocument
//authorization master ( instance )
{
  mapping for zdt_ekpo
  {
    PurchasingDocumentItemId = purg_doc_item_id;
    PurchasingDocumentId = purg_doc_id;
    PurchasingDocument = ebeln;
    PurchasingDocumentItem = ebelp;
    Material = matnr;
    PurchasingDocumentItemText = txz01;
    OrderQuantity = menge;
    OrderQuantityUnit = meins;
    NetPriceAmount = netpr;
    CreatedBy = created_by;
    CreatedAt = created_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }

  field ( readonly, numbering : managed ) PurchasingDocumentItemId;

  field( readonly ) PurchasingDocumentId, PurchasingDocument, PurchasingDocumentItem, DocumentCurrency;

  //field( mandatory ) OrderQuantity, NetPriceAmount;

  update;
  delete;

  determination setItemNumber on save { create; }
  determination calculateNetAmount on modify { field OrderQuantity, NetPriceAmount; }

  association _PurchasingDocument;
}