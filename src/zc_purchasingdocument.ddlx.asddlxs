@Metadata.layer: #CORE

@UI.headerInfo: { 
  typeName: 'Purchase Order', 
  typeNamePlural: 'Purchase Orders',
  title: { type: #STANDARD, value: 'PurchasingDocument' } 
}

@Search.searchable: true
annotate view ZC_PurchasingDocument with 
{
  @UI.facet: [{ 
    id:'GeneralInformationCollectionFacet',
    type:#COLLECTION,
    label:'General Information',
    purpose: #STANDARD
  },
  
  { 
    id:'FieldGroupReferenceFacet1',
    type:#FIELDGROUP_REFERENCE,
    parentId: 'GeneralInformationCollectionFacet',
    purpose: #STANDARD,
    targetQualifier: 'FieldGroup1'
  },
  
  { 
    id:'FieldGroupReferenceFacet2',
    type:#FIELDGROUP_REFERENCE,
    parentId: 'GeneralInformationCollectionFacet',
    purpose: #STANDARD,
    targetQualifier: 'FieldGroup2'
  },
   
  { 
    id:'FieldGroupReferenceFacet3',
    type:#FIELDGROUP_REFERENCE,
    parentId: 'GeneralInformationCollectionFacet',
    purpose: #STANDARD,
    targetQualifier: 'FieldGroup3'
  },   
   
  {
    id:'ItemsLineItemReferenceFacet',
    type: #LINEITEM_REFERENCE,
    purpose: #STANDARD,
    label: 'Items',
    targetElement: '_PurchasingDocumentItem'
  }]

  @UI: {
    lineItem: [ { position: 10, importance: #HIGH, label: 'Purchase Order' } ],
    identification: [ { position: 10, label: 'Purchase Order' } ]
  }      
  @Search.defaultSearchElement: true  
  PurchasingDocument;
  
  @UI.selectionField: [{ position: 20 }]
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_Supplier', element: 'Supplier' } }]
  @UI.fieldGroup: [{ position: 10, label: 'Supplier', qualifier: 'FieldGroup1' }]
  @UI.lineItem: [ { position: 20, importance: #HIGH, label: 'Supplier' } ]  
  Supplier;
  
  @UI.hidden: true
  SupplierName;
  
  @UI.selectionField: [{ position: 30 }]
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_CompanyCode', element: 'CompanyCode' } }]
  @UI.fieldGroup: [{position: 30, label: 'Company Code', qualifier: 'FieldGroup3' }]
  @UI.lineItem: [ { position: 30, importance: #HIGH, label: 'Company Code' } ]
  CompanyCode;
  
  @UI.hidden: true
  CompanyCodeName;
  
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_PurchasingOrganization', element: 'PurchasingOrganization' } }]
  @UI.fieldGroup: [{position: 10, label: 'Purchasing Organization', qualifier: 'FieldGroup2' }]
  PurchasingOrganization;
  
  @UI.hidden: true
  PurchasingOrganizationName;  
  
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_PurchasingGroup', element: 'PurchasingGroup' } }]
  @UI.fieldGroup: [{position: 20, label: 'Purchasing Group', qualifier: 'FieldGroup2' }]
  PurchasingGroup;
  
  @UI.hidden: true
  PurchasingGroupName;
}