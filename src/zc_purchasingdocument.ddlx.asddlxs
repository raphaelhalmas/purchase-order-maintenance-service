@Metadata.layer: #CORE
@Search.searchable: true

@UI.headerInfo: {
  title: {
    type: #STANDARD, value: 'PurchasingDocument' 
  },

  typeName: 'Display Purchase Order', 
  typeNamePlural: 'Purchase Orders'
}

@UI.presentationVariant: [{ sortOrder: [{ by: 'PurchasingDocument', direction:  #ASC }] }]
annotate view ZC_PurchasingDocument with
{
  @UI.facet: [{ 
    id:'GeneralInformationCollectionFacet',
    label:'General Information',    
    purpose: #STANDARD,
    type:#COLLECTION    
  },
  
  { 
    id:'FieldGroupReferenceFacet1',    
    label: 'General Data',    
    parentId: 'GeneralInformationCollectionFacet',
    purpose: #STANDARD,    
    targetQualifier: 'FieldGroup1',
    type:#FIELDGROUP_REFERENCE
  },
  
  { 
    id:'FieldGroupReferenceFacet2',
    label: 'Organizational Data',    
    parentId: 'GeneralInformationCollectionFacet',    
    purpose: #STANDARD,    
    targetQualifier: 'FieldGroup2',
    type:#FIELDGROUP_REFERENCE
  },
  
  {
    id:'ItemsLineItemReferenceFacet',
    label: 'Items',
    purpose: #STANDARD,
    targetElement: '_PurchasingDocumentItem',
    type: #LINEITEM_REFERENCE
  }]

  @UI.hidden: true
  PurchasingDocumentId;

  @Search.defaultSearchElement: true
  @UI.lineItem: [{ position: 10, importance: #HIGH, label: 'Purchase Order' }, { position: 10, label: 'Adopt', type: #FOR_ACTION, dataAction: 'copyPurchasingDocument' }]
  PurchasingDocument;
  
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_PurchasingDocumentType', element: 'PurchasingDocumentType' } }]
  @UI.fieldGroup: [{ position: 10, label: 'Order Type', qualifier: 'FieldGroup1' }]
  PurchasingDocumentType;
  
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_Supplier', element: 'Supplier' } }]    
  @UI.fieldGroup: [{ position: 20, label: 'Supplier', qualifier: 'FieldGroup1' }]
  @UI.lineItem: [{ position: 20, importance: #HIGH, label: 'Supplier' }]
  @UI.selectionField: [{ position: 20 }]
  @UI.textArrangement: #TEXT_FIRST
  Supplier;  
 
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_CompanyCode', element: 'CompanyCode' } }]
  @UI.fieldGroup: [{ position: 10, label: 'Company Code', qualifier: 'FieldGroup2' }]
  @UI.lineItem: [{ position: 30, importance: #HIGH, label: 'Company Code' }]
  @UI.selectionField: [{ position: 40 }]
  @UI.textArrangement: #TEXT_FIRST
  CompanyCode;
 
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_PurchasingOrganization', element: 'PurchasingOrganization' } }]
  @UI.fieldGroup: [{ position: 20, label: 'Purchasing Organization', qualifier: 'FieldGroup2' }]
  @UI.textArrangement: #TEXT_FIRST
  PurchasingOrganization;
  
  @Consumption.valueHelpDefinition: [{ entity: { name: 'ZI_PurchasingGroup', element: 'PurchasingGroup' } }]
  @UI.fieldGroup: [{ position: 30, label: 'Purchasing Group', qualifier: 'FieldGroup2' }]
  @UI.selectionField: [{ position: 30 }]
  @UI.textArrangement: #TEXT_FIRST
  PurchasingGroup;
  
  @UI.fieldGroup: [{ position: 40, label: 'Net Order Value', qualifier: 'FieldGroup1', importance: #HIGH }]
  @UI.lineItem: [{ importance: #HIGH, position: 50, label: 'Net Order Value'}]
  PurchasingDocumentNetAmount;

  @UI.fieldGroup: [{ position: 30, label: 'Doc. Date', qualifier: 'FieldGroup1' }]
  @UI.lineItem: [ { position: 40, importance: #HIGH, label: 'Doc. Date' } ]  
  PurchasingDocumentDate;  
}